=if(
  isblank(
    $O$3
  ),,iferror(
    arrayformula(
      if(
        and(
          Q3="Armor Class",isnumber(
            V3
          )
        ),V3,vlookup(
          V3,$J$3:$L$8,3,false
        )
      )+iferror(
        if(
          and(
            isblank(
              W3
            ),regexmatch(
              Q3,"Acrobatics|Arcana|Athletics|Crafting|Deception|Diplomacy|Intimidation|Medicine|Nature|Occultism|Performance|Religion|Society|Stealth|Survival|Thievery|Lore"
            ),or(
              regexmatch(
                $I$51:$I$56,"(?i)Untrained Improvisation"
              )
            )
          ),if(
            $O$3>=7,$O$3,floor(
              $O$3/2
            )
          ),vlookup(
            W3,$O$5:$P$8,2,false
          )
        )
      )+sum(
        iferror(
          value(
            regexextract(
              {
                $A$43:$A$77;$I$42:$I$48;$I$51:$I$56;$I$59:$I$70;$Q$42:$Q$56;$Q$59:$Q$70;$I$73:$I$79
              },"(?:^| |[[:punct:]])(?:\+)([0-9])(?: )(?i:"&Q3&if(
                regexmatch(
                  Q3,".* Lore"
                ),,if(
                  Q3="Spell",ifs(
                    indirect(
                      address(
                        row()-1,column()
                      )
                    )="Atk","|SpellAttack|SpAtk",indirect(
                      address(
                        row()-1,column()
                      )
                    )="DC","|SpellDC|SpDC"
                  ),switch(
                    Q3,"Armor Class","|AC","Fortitude","|Fort|saves","Reflex","|Ref|saves","Will","|saves","Perception","|Prc","Intimidation","|Itm","Performance","|Prf","Class DC","|CDC","|"&left(
                      Q3,3
                    )
                  )
                )
              )&")(?:$| |[[:punct:]])"
            )
          )
        )
      )+if(
        or(
          Q3="Armor Class",Q3="Class DC",and(
            Q3="Spell",indirect(
              address(
                row()-1,column()
              )
            )="DC"
          )
        ),10
      )+if(
        Q3="Armor Class",sum(
          iferror(
            value(
              regexextract(
                $A$43:$A$77,"(?:^| |[[:punct:]])(?:\+)([0-9])(?:.*)(?: |[[:punct:]])(?:\+[0-9] )(?:Armor Class|AC)(?:$| |[[:punct:]])"
              )
            )
          ),
          iferror(
            value(
              regexextract(
                $A$43:$A$77,"(?:^| |[[:punct:]])(?:\+)([0-9])(?: [A-Za-z\.\-' ]*)(?i:Explorer's clothing)"
              )
            )
          )
        )
      )+if(
        or(
          Q3="Fortitude",Q3="Reflex",Q3="Will"
        ),iferror(
          ifs(
            or(
              regexmatch(
                $A$43:$A$77,"(?:^| |[[:punct:]])((?i)major resilient|maj\.res\.)(?:.*)(?: |[[:punct:]])(?:\+[0-9] )(?:Armor Class|AC)(?:$| |[[:punct:]])"
              )
            ),3,or(
              regexmatch(
                $A$43:$A$77,"(?:^| |[[:punct:]])((?i)greater resilient|grt\.res\.)(?:.*)(?: |[[:punct:]])(?:\+[0-9] )(?:Armor Class|AC)(?:$| |[[:punct:]])"
              )
            ),2,or(
              regexmatch(
                $A$43:$A$77,"(?:^| |[[:punct:]])((?i)resilient|res\.)(?:.*)(?: |[[:punct:]])(?:\+[0-9] )(?:Armor Class|AC)(?:$| |[[:punct:]])"
              )
            ),1
          )
        )
      )+if(
        or(
          Q3="Armor Class",Q3="Fortitude",Q3="Reflex",Q3="Will"
        ),sum(
          iferror(
            value(
              regexextract(
                $A$43:$A$77,"(?:^| |[[:punct:]])(?:\+)([0-9])(?i: Bracers of Armor)"
              )
            )
          )
        )
      )-if(
        or(
          Q3="Stealth",Q3="Thievery",and(
            not(
              or(
                regexmatch(
                  {
                    $A$43:$A$77;$I$42:$I$48;$I$51:$I$56;$I$59:$I$70;$Q$42:$Q$56;$Q$59:$Q$70;$I$73:$I$79
                  },"(?:^| |[[:punct:]])(?:\-[0-9] )(?i:Armor Check Penalty|ACP)(?: |[[:punct:]])(?:.*)(?: |[[:punct:]])((?i)flexible|flex)(?:$| |[[:punct:]])"
                )
              )
            ),or(
              Q3="Acrobatics",Q3="Athletics"
            )
          )
        ),sum(
          iferror(
            value(
              regexextract(
                {
                  $A$43:$A$77;$I$42:$I$48;$I$51:$I$56;$I$59:$I$70;$Q$42:$Q$56;$Q$59:$Q$70;$I$73:$I$79
                },"(?:^| |[[:punct:]])(?:\-)([0-9])(?: )(?i:Armor Check Penalty|ACP)(?:$| |[[:punct:]])"
              )
            )
          )
        )
      )
    )
  )
)